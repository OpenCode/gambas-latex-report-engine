' Gambas class file

' -----------------------------------------------------------------------------
'
'  LATEX REPORT ENGINE
'  
'  Copyright(c) 2014 Francesco OpenCode Apruzzese All Rights Reserved.
'  www.e-ware.org
'  opencode@e-ware.org
'
'  This program Is Free software: you can redistribute it And / Or modify
'  it under the terms Of the GNU Affero General Public License As Published
'  by the Free Software Foundation, either version 3 Of the License, Or
'  (at your option)any later version.
'
'  This program Is Distributed In the hope that it will be useful,
'  but WITHOUT ANY WARRANTY; without even the implied warranty Of
'  MERCHANTABILITY Or FITNESS For A PARTICULAR PURPOSE.See the
'  GNU Affero General Public License For more details.
'
'  You should have received a Copy Of the GNU Affero General Public License
'  along With this program. If Not ,see <http:/www.gnu.org/licenses>
'
' -----------------------------------------------------------------------------

' ---------
' VARIABLES
' ---------

' ----- Content datas
Private begin_content As String
Private content As String
Private end_content As String
Private list_type As String

' ---------
' CONSTANTS
' ---------

' ----- Report datas
Public Const Itemize As String = "itemize"
Public Const Enumerate As String = "enumerate"
Public Const Description As String = "description"
' ----- Control Datas
Private ValidlListType As String[] = [Itemize, Enumerate, Description]

' -------------------
' LIST CORE functions
' -------------------

Public Sub _new(Optional type As String = "itemize", Optional values As Variant[])

    Dim value As Variant

    content = ""
    SetType(type)
    list_type = type
    If values <> Null Then
        For Each value In values
            AddElement(value)
        Next
    Endif

End

Public Sub AddElement(value As Variant)

    If TypeOf(value) == gb.String Then
        content = content & "\\item " & value & gb.NewLine
    Else
        content = content & "\\item[" & value[0] & "]" & value[1] & gb.NewLine
    Endif

End


' -----------------
' SETTING functions
' -----------------

Public Sub SetType(Optional type As String = "")

    ' ----- Check the type of the list passed as argument
    If type And Not ValidlListType.Exist(type) Then
        Error.Raise("Not defined list type for " & type)
    Endif
    ' ----- Set parameters
    list_type = IIf(type <> "", type, list_type)
    begin_content = "\\begin{" & type & "}"
    end_content = "\\end{" & type & "}"
    
End

' -----------------
' GETTING functions
' -----------------

Public Sub GetType() As String
    
    Return list_type
    
End

Public Sub GetList() As String
    
    Return begin_content & gb.NewLine & content & end_content & gb.NewLine
    
End

Public Sub Include() As String
    
    Return GetList()  
    
End